// SPDX-License-Identifier: BSD-3-Clause
/*
 *                          88  88                                   88
 *                          88  88                            ,d     ""
 *                          88  88                            88
 *  ,adPPYba,   ,adPPYba,   88  88   ,adPPYba,   ,adPPYba,  MM88MMM  88  8b       d8   ,adPPYba,
 * a8"     ""  a8"     "8a  88  88  a8P_____88  a8"     ""    88     88  `8b     d8'  a8P_____88
 * 8b          8b       d8  88  88  8PP"""""""  8b            88     88   `8b   d8'   8PP"""""""
 * "8a,   ,aa  "8a,   ,a8"  88  88  "8b,   ,aa  "8a,   ,aa    88,    88    `8b,d8'    "8b,   ,aa
 *  `"Ybbd8"'   `"YbbdP"'   88  88   `"Ybbd8"'   `"Ybbd8"'    "Y888  88      "8"       `"Ybbd8"'
 *
 */
/*
 * BSD 3-Clause License
 *
 * Copyright (c) 2023, collective
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice, this
 *    list of conditions and the following disclaimer.
 *
 * 2. Redistributions in binary form must reproduce the above copyright notice,
 *    this list of conditions and the following disclaimer in the documentation
 *    and/or other materials provided with the distribution.
 *
 * 3. Neither the name of the copyright holder nor the names of its
 *    contributors may be used to endorse or promote products derived from
 *    this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
pragma solidity ^0.8.15;

/// @notice User defined metadata
struct Meta {
    /// @notice metadata key or name
    bytes32 name;
    /// @notice metadata value
    string value;
}

// solhint-disable-next-line func-visibility
function getHash(Meta memory meta) pure returns (bytes32) {
    return keccak256(abi.encode(meta));
}

/// @title dynamic collection of metadata
contract MetaSet {
    error IndexInvalid(uint256 index);
    error HashCollision(bytes32 txId);

    event MetaAdded(bytes32 metaHash);
    event MetaRemoved(bytes32 metaHash);

    uint256 private _elementCount;

    mapping(uint256 => Meta) private _elementMap;

    mapping(bytes32 => uint256) private _elementPresent;

    constructor() {
        _elementCount = 0;
    }

    modifier requireValidIndex(uint256 index) {
        if (index == 0 || index > _elementCount) revert IndexInvalid(index);
        _;
    }

    /// @notice add meta
    /// @param _element the meta
    /// @return uint256 the elementId of the meta
    function add(Meta memory _element) external returns (uint256) {
        uint256 elementIndex = ++_elementCount;
        _elementMap[elementIndex] = _element;
        bytes32 _elementHash = getHash(_element);
        if (_elementPresent[_elementHash] > 0) revert HashCollision(_elementHash);
        _elementPresent[_elementHash] = elementIndex;
        emit MetaAdded(_elementHash);
        return elementIndex;
    }

    function erase(Meta memory _meta) public returns (bool) {
        bytes32 metaHash = getHash(_meta);
        uint256 index = _elementPresent[metaHash];
        return erase(index);
    }

    /// @notice erase a meta
    /// @param _index the index to remove
    /// @return bool True if element was removed
    function erase(uint256 _index) public returns (bool) {
        Meta memory meta = _elementMap[_index];
        bytes32 metaHash = getHash(meta);
        uint256 elementIndex = _elementPresent[metaHash];
        if (elementIndex > 0 && elementIndex == _index) {
            Meta memory _lastMeta = _elementMap[_elementCount];
            _elementMap[elementIndex] = _lastMeta;
            bytes32 _lastMetaHash = getHash(_lastMeta);
            _elementPresent[_lastMetaHash] = elementIndex;
            _elementMap[_elementCount] = Meta("", "");
            _elementPresent[metaHash] = 0;
            delete _elementMap[_elementCount];
            delete _elementPresent[metaHash];
            _elementCount--;
            emit MetaRemoved(metaHash);
            return true;
        }
        return false;
    }

    /// @return uint256 The size of the set
    function size() external view returns (uint256) {
        return _elementCount;
    }

    /// @param index The index to return
    /// @return address The requested address
    function get(uint256 index) external view requireValidIndex(index) returns (Meta memory) {
        return _elementMap[index];
    }

    /// @param _meta The element to test
    /// @return bool True if address is contained
    function contains(Meta memory _meta) external view returns (bool) {
        return find(_meta) > 0;
    }

    /// @param _meta The element to find
    /// @return uint256 The index associated with element
    function find(Meta memory _meta) public view returns (uint256) {
        bytes32 metaHash = getHash(_meta);
        return _elementPresent[metaHash];
    }
}
